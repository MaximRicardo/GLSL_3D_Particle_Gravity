#version 430 core

layout (std430, binding=0) buffer particle_positions_buffer {
    vec4 particle_positions[];
};

layout (std430, binding=1) buffer particle_lighting_buffer {
    float particle_lighting[];
};

layout (std430, binding=2) buffer particle_velocities_buffer {
    vec4 particle_velocities[];
};

layout (std430, binding=4) readonly buffer particle_radii_buffer {
    float particle_radii[];
};

uniform float particle_mass;
uniform float particle_light_strength;

uniform float G;

uniform float delta_time;

uniform int n_particles;

float PI = 3.141592;

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

void gravity(int thread_idx, int other_idx, inout vec3 acceleration) {

    float dist = distance(particle_positions[thread_idx].xyz, particle_positions[other_idx].xyz);
    if (dist == 0) return;

    float f = particle_mass*G/pow(dist, 2.0);    
    vec3 dir = normalize(particle_positions[other_idx].xyz - particle_positions[thread_idx].xyz);

    acceleration.xyz += dir*f;

}

void collision(int thread_idx, int other_idx) {

    float self_radius = particle_radii[thread_idx];
    float other_radius = particle_radii[other_idx];
    float radius_sum = self_radius+other_radius;

    float dist = distance(particle_positions[thread_idx].xyz, particle_positions[other_idx].xyz);
    if (dist >= radius_sum) return;

    float move_back_amount = radius_sum - dist;
    vec3 move_back_dir = particle_positions[thread_idx].xyz-particle_positions[other_idx].xyz;
    if (move_back_dir.length() == 0.0) return;
    move_back_dir = normalize(move_back_dir);

    particle_positions[thread_idx].xyz += move_back_dir*move_back_amount;

    vec3 n = -move_back_dir;
    float this_d = dot(particle_velocities[thread_idx].xyz, n);
    if (this_d > 0.0) {
        particle_velocities[thread_idx].xyz -= n*this_d;

        particle_velocities[other_idx].xyz += n*this_d/1.1;
    }

}

void main() {

    int thread_idx = int(gl_GlobalInvocationID.x);
    if (thread_idx >= n_particles) return;

    particle_lighting[thread_idx] = 0.0;
    vec3 acceleration = vec3(0.0, 0.0, 0.0);

    for (int i = 0; i < n_particles; i++) {
        if (i == thread_idx) continue;
        //collision(thread_idx, i);
        gravity(thread_idx, i, acceleration);
        float dist_squared = pow(distance(particle_positions[thread_idx].xyz, particle_positions[i].xyz), 2.0);
        if (dist_squared != 0.0) particle_lighting[thread_idx] += (2.0*PI*pow(particle_radii[i],2.0))*particle_light_strength / dist_squared;
    }

    //Apply acceleration and velocity
    particle_velocities[thread_idx].xyz += acceleration.xyz*delta_time;
    particle_positions[thread_idx].xyz += particle_velocities[thread_idx].xyz*delta_time;
    
}
